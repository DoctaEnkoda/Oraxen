import java.text.SimpleDateFormat

plugins {
    id "java"
    id "idea"
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

group "io.th0rgal"
def pluginVersion = "1.34.0"

repositories {
    mavenCentral()
    mavenLocal()
    //Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    //Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    //CodeMc (bstats)
    maven { url "https://repo.codemc.org/repository/maven-public" }
    //ProtocolLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    //Minecraft repo (commodore)
    maven { url "https://libraries.minecraft.net/" }
    //MythicMobs
    maven { url "https://mvn.lumine.io/repository/maven-releases/" }
    //sk89q-repo
    maven { url "https://maven.enginehub.org/repo/" }
    jcenter() // needed for building shadow from protocolLib 4.5.0
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = "UTF-8"
}

dependencies {
    // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly group: "org.spigotmc", name: "spigot", version: "1.14.4-R0.1-SNAPSHOT"
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:LATEST'
    compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.14.4-R0.1-SNAPSHOT"
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "+"
    compileOnly group: "io.lumine.xikage", name: "MythicMobs", version: "4.9.1"
    implementation group: 'com.github.rockswang', name: 'java-curl', version: '1.2.2.2'
    implementation group: "me.lucko", name: "commodore", version: "1.3"
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '+'
}

processResources {
    filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
        expand projectVersion: pluginVersion
    }
}

shadowJar {
    relocate "org.bstats.bukkit", "io.th0rgal.oraxen.bstats"
    relocate "me.lucko:commodore", "io.th0rgal.oraxen.commodore"
    minimize { exclude("io.th0rgal.oraxen.api.*") }
    archiveClassifier.set(pluginVersion)
    archiveClassifier.set(pluginVersion)

    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

task buildJar(dependsOn: [clean, shadowJar])
